subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : modid,
                    'Specification-Vendor'    : 'azmalent',
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : modid,
                    'Implementation-Vendor'   : 'azmalent',
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestamp'               : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version,
                    
                    'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                    "MixinConfigs": "${modid}.mixins.json",
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven { //Botania
            name = 'blamejared'
            url = 'https://maven.blamejared.com/'
        }

        maven { // Curios
            name "C4"
            url "https://maven.theillusivec4.top/"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }
}